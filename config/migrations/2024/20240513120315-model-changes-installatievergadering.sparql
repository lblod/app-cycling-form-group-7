PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX lmb: <http://lblod.data.gift/vocabularies/lmb/>

DELETE {
  GRAPH ?g {
    ?installatieVergadering ext:hasBestuursorgaanInDeTijd ?bestuursorgaanInDeTijd.
  }
}
INSERT {
  GRAPH ?g {
    ?installatieVergadering lmb:heeftBestuurseenheid ?bestuurseenheid;
                            lmb:heeftBestuursperiode ?periode.
  }
}
WHERE {
  GRAPH ?g {
    ?installatieVergadering ext:hasBestuursorgaanInDeTijd ?bestuursorgaanInDeTijd.
    ?bestuursorgaanInDeTijd mandaat:isTijdspecialisatieVan ?orgaan.
    ?bestuursorgaanInDeTijd lmb:heeftBestuursperiode ?periode.
    ?orgaan besluit:bestuurt ?bestuurseenheid.
  }
}