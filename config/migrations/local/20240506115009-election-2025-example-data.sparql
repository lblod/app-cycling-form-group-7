prefix mandaat:	<http://data.vlaanderen.be/ns/mandaat#> 
prefix bestuursorgaan: <http://data.lblod.info/id/bestuursorganen/>
prefix verkiezingen: <http://data.lblod.info/id/rechtstreekse-verkiezingen/>
prefix xsd:	<http://www.w3.org/2001/XMLSchema#>
prefix ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
  GRAPH ?g {
    ?installatieVergadering ext:hasBestuursorgaanInDeTijd ?bestuursorgaanInDeTijd .
    ?verkiezing mandaat:steltSamen ?bestuursorgaanInDeTijd .
    ?verkiezing mandaat:datum ?datum .
  }
}
INSERT {
  GRAPH ?g {
    ?verkiezing mandaat:steltSamen ?huidigBesuursorgaanInDeTijd .
    ?installatieVergadering ext:hasBestuursorgaanInDeTijd ?huidigBesuursorgaanInDeTijd .
    ?verkiezing mandaat:datum "2025-01-01"^^xsd:date.
  }
}

WHERE {
  GRAPH ?g {
    ?installatieVergadering ext:hasBestuursorgaanInDeTijd ?bestuursorgaanInDeTijd .
    ?verkiezing mandaat:steltSamen ?bestuursorgaanInDeTijd .
    ?verkiezing mandaat:datum ?datum .
    ?bestuursorgaanInDeTijd mandaat:isTijdspecialisatieVan ?orgaan .
    ?huidigBesuursorgaanInDeTijd mandaat:isTijdspecialisatieVan ?orgaan .
    ?huidigBesuursorgaanInDeTijd mandaat:bindingStart "2025-01-01"^^xsd:date .
  }
}